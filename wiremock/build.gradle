plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.springframework.restdocs:spring-restdocs-core:1.2.2.RELEASE"
    compile "com.jayway.jsonpath:json-path:2.3.0"
    compileOnly 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'
    testCompile 'uk.co.datumedge:hamcrest-json:0.2'
    testCompile 'com.google.guava:guava:23.0'
}

group = 'com.epages'
description = 'Adds WireMock stub snippets to Spring REST Docs'
sourceCompatibility = 1.7
targetCompatibility = 1.7

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.coveralls {
    dependsOn 'check'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task setupVariables << {
    def nexusUsername = project.findProperty("nexusUsername") ?: System.getenv("nexusUsername")
    def nexusPassword = project.findProperty("nexusPassword") ?: System.getenv("nexusPassword")
    def signingKeyId = project.findProperty("signing.keyId") ?: System.getenv("SIGNING_KEY_ID")
    def signingPassword = project.findProperty("signing.password") ?: System.getenv("SIGNING_PASSWORD")
    def signingSecretKeyRingFile = project.findProperty("signing.secretKeyRingFile") ?: System.getenv("SIGNING_SECRET_KEY_RING_FILE")

    println "Key is null: ${signingKeyId == null} "

    def file = new File(signingSecretKeyRingFile)

    print "File exists: ${file.exists()}"

    if (!nexusUsername || !nexusPassword) {
        throw new RuntimeException("nexusUsername and/or nexusPassword are not defined environment variables")
    }

    if (!signingKeyId || !signingPassword || !signingSecretKeyRingFile) {
        throw new RuntimeException("signing keyId and/or password and/or secretKeyRingFile are not defined environment variables")
    }

    System.properties.setProperty("nexusUsername", nexusUsername)
    System.properties.setProperty("nexusPassword", nexusPassword)
    System.properties.setProperty("signing.keyId", signingKeyId)
    System.properties.setProperty("signing.password", signingPassword)
    System.properties.setProperty("signing.secretKeyRingFile", signingSecretKeyRingFile)
}


uploadArchives.dependsOn('setupVariables')

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'restdocs-wiremock'
            groupId = project.group

            artifact sourceJar {
                classifier 'sources'
            }
            artifact javadocJar
        }
    }
}
apply plugin: 'com.bmuschko.nexus'

archivesBaseName = 'restdocs-wiremock'
group = project.group
version = project.version

modifyPom {
    project {
        name 'restdocs-wiremock'
        description 'Spring REST Docs WireMock Integration.'
        url 'https://github.com/ePages-de/restdocs-wiremock'
        inceptionYear '2021'

        scm {
            url 'https://github.com/ePages-de/restdocs-wiremock'
            connection 'scm:https://github.com/ePages-de/restdocs-wiremock.git'
            developerConnection 'scm:git://github.com/ePages-de/restdocs-wiremock.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ePages'
                name 'ePages'
                email 'ePages@ePages.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}
